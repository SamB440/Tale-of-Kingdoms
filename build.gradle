plugins {
    id 'groovy'
    id 'fabric-loom' version '1.1-SNAPSHOT'
    id 'maven-publish'
}

java.sourceCompatibility = JavaVersion.VERSION_17
java.targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

repositories {
    mavenLocal()
    mavenCentral()
    maven { url "https://maven.shedaniel.me/" }
    maven { url 'https://maven.wispforest.io' }
    maven { url "https://maven.terraformersmc.com/" }
    maven { url "https://server.bbkr.space/artifactory/libs-release" }
    maven { url "https://jitpack.io" }
}

sourceSets {
    // Add a data gen sourceset
    datagen {
        compileClasspath += main.compileClasspath
        runtimeClasspath += main.runtimeClasspath
        compileClasspath += main.output
        runtimeClasspath += main.output
    }

    main {
        resources {
            exclude 'assets/schematics'
            srcDirs += [
                    'src/main/generated'
            ]
        }
    }
}

loom {
    runs {
        datagen {
            server()
            name "Data Generation"
            vmArg "-Dfabric-api.datagen"
            vmArg "-Dfabric-api.datagen.output-dir=${file("src/main/generated")}"
            vmArg "-Dfabric-api.datagen.modid=${modid}"
            runDir "build/datagen"
        }
    }
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter:5.9.0'
    testImplementation 'com.google.code.gson:gson:2.10'
    testImplementation 'org.reflections:reflections:0.10.2'

    compileOnly 'org.jetbrains:annotations:23.0.0'

    //to change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    // Fabric API. This is technically optional, but you probably want it anyway.
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    // PSA: Some older mods, compiled on Loom 0.2.1, might have outdated Maven POMs.
    // You may need to force-disable transitiveness on them.

    // I took a lot of this from dynamic fps just for examples

    // Mod Menu, to add the hook for a config screen
    modImplementation "com.terraformersmc:modmenu:${project.modmenu_version}"

    annotationProcessor modImplementation("io.wispforest:owo-lib:${project.owo_version}")
    modCompileOnly include("io.wispforest:owo-sentinel:${project.owo_version}")

    modApi("me.shedaniel.cloth:cloth-config-fabric:${project.cloth_version}") {
        exclude(group: "net.fabricmc.fabric-api")
    }
    include("me.shedaniel.cloth:cloth-config-fabric:${project.cloth_version}") {
        exclude(group: "net.fabricmc.fabric-api")
    }

    // Thanks Mojang!
    datagenImplementation('org.apache.groovy:groovy:4.0.7')
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }

    from("CHANGELOG.md") {
        include()
    }
}

tasks {
    jar {
        // I don't care about your datagen
        duplicatesStrategy = DuplicatesStrategy.WARN
        from file('src/main/generated')
        from("LICENSE") {
            rename { "${it}_${project.archivesBaseName}"}
        }
        dependsOn 'runDatagen'
    }

    test {
        useJUnitPlatform()

        testLogging {
            events("passed", "skipped", "failed")
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    // ensure that the encoding is set to UTF-8, no matter what the system default is
    // this fixes some edge cases with special characters not displaying correctly
    // see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
    // If Javadoc is generated, this must be specified in that task too.
    it.options.encoding = "UTF-8"

    // Force warnings
    options.compilerArgs << '-Xlint:all'
    options.deprecation = true

    // Minecraft 1.18 (1.18-pre2) upwards uses Java 17.
    it.options.release = 17
}

java {
    // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
    // if it is present.
    // If you remove this line, sources will not be generated.
    withSourcesJar()
}

// configure the maven publication
publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }

    // select the repositories you want to publish to
    repositories {
        // uncomment to publish to the local maven
        // mavenLocal()
    }
}